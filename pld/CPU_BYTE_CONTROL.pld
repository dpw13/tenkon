GAL22V10
BYTE_CONTROL

; Name     CPU_CONTROL_SIGNALS
; PartNo   00
; Date     3/29/2023
; Revision 01
; Designer Dylan Enloe
; Company  Test
; Assembly None
; Location
; Device   p22v10

NC           RW           /AS           /DS           A0            A1            SIZ0          SIZ1          /EXTCS        /BG           /BUSBERR     GND
WATCHDOGBERR BUSDIR       /NOTAS        /READ         /UUCS         /UMCS         /LMCS         /LLCS         /BUSOE        /NOTBG        /BERR        VCC

NOTAS = /AS
NOTBG = /BG

; Bus writes occur during the data phase of writes to upper addresses, assuming
; no bus grant is active.
; Bus transceivers should be enabled when we are in the data phase of an external access.
BUSOE = /BG * EXTCS * DS
; Bus direction is 0 when the CPU is driving the bus, 1 when the bus is driving data to the CPU
BUSDIR = /BG * /RW

READ = RW

BERR = BUSBERR + WATCHDOGBERR

; These are actually write enables. The /SRAMSELECT signal keeps these chips in powerdown
; until they're accessed, so we don't check the upper address bits here.

; D31-D24, bottom byte in memory. Wrapping accesses across word boundaries are not allowed,
; so the only way to access this byte is directly (0b00).
UUCS = /RW * AS * /A0 * /A1

; D16-23, byte 0b01.
UMCS = /RW * AS * A0 * /A1    ; Direct access
     + /RW * AS * /A1 * /SIZ0 ; Access to 0b0X with size 1 or 3
     + /RW * AS * /A1 * SIZ1  ; Access to 0b0X with size 2 or 4

; D8-15, byte 0b10
LMCS = /RW * AS * /A0 * A1              ; Direct access
     + /RW * AS * /A1 * /SIZ0 * /SIZ1   ; Access to 0b0X with size 4
     + /RW * AS * /A1 * SIZ0 * SIZ1     ; Access to 0b0X with size 3
     + /RW * AS * /A1 * A0 * /SIZ0      ; Access to 0b01 with size 1 or 3

; D0-7, byte 0b11
LLCS = /RW * AS * A0 * A1          ; Direct access
     + /RW * AS * A0 * SIZ0 * SIZ1 ; Access to 0bX1 with size 3
     + /RW * AS * /SIZ0 * /SIZ1    ; Access to any address with size 4
     + /RW * AS * A1 * SIZ1        ; Access to 0b1X with size 1 or 3

